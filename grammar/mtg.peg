Rule
    = (AbilityWord " â€” ")? Ability
    ;

Ability
    = SpellEffect EOR
    / KeywordAbility EOR
    / ActivatedAbility EOR
    / TriggeredAbility EOR
    ;

SpellEffect
    = Effect EOS (SPACE Effect EOS)*
    ;

Effect
    = SingleEffect " and " SingleEffect
    / SingleEffect ", then " SingleEffect
    / SingleEffect
    ;

SingleEffect
    = (Duration ", ")? (Optional SPACE)? (SelectPlayer SPACE)? Action (SPACE Duration)?
    ;

Optional
    = "you may have"
    / "you may"
    / "if you don't,"
    / "if you can't,"
    / "if you do,"
    ;

AbilityWord
    = "metalcraft"
    ;

KeywordAbility
    = Keyword
    / Enchant
    ;

ActivatedAbility
    = ActivationCosts ": " SpellEffect
    ;

ActivationCosts
    = Action ("," SPACE Action)*
    ;

TriggeredAbility
    = Trigger ", " SpellEffect
    ;

Trigger
    = EntersBattlefieldTrigger
    / LeavesBattlefieldTrigger
    / EntersGraveyardTrigger
    / BeginningUpkeepTrigger
    / BeginningEndStepTrigger
    / DiesTrigger
    / DealsDamageTrigger
    / CastTrigger
    / BecomesTargetTrigger
    / BlocksTrigger
    / AttacksTrigger
    / BlockedTrigger
    ;

Action
    = DestroyNoRegenAction
    / DestroyAction 
    / ExileAction
    / CounterAction
    / DamageAction
    / DrawAction
    / DiscardAction
    / TapAction
    / PreventAction
    / RegenerateAction
    / PumpAction
    / GainAction
    / BounceAction
    / AddManaAction
    / ReturnAction
    / SacrificeAction
    / ChangeLifeAction
    / ChangeCounterAction
    / PutTokenAction
    / ControlAction
    / ShuffleAction
    / DoesntUntapAction
    / EntersTapped
    / EntersWithCounter
    / PayManaAction
    / MillAction
    / ReanimateAction
    / PoisonAction
    / SetPTAction
    / ChangeStateAction
    ;

Keyword
    = "flash"
    / "flying"
    / "haste"
    / "defender"
    / "vigilance"
    / "reach"
    / "battle cry"
    / "trample"
    / "first strike"
    / "double strike"
    / "infect"
    / "wither"
    / "fear"
    / "shadow"
    / "storm"
    / "swampwalk"
    / "plainswalk"
    / "forestwalk"
    / "islandwalk"
    / "mountainwalk"
    / "changeling"
    / "deathtouch"
    / "lifelink"
    / "exalted"
    / "shroud"
    / "persist"
    / "protection from all colors"
    / "protection from black"
    / "protection from red"
    / "protection from white"
    / "protection from blue"
    / "protection from green"
    / "protection from artifacts"
    / "protection from creatures"
    / "protection from everything"
    / "hexproof"
    / "soulbond"
    / "undying"
    / "flanking"
    / "intimidate"
    / "living weapon"
    / "totem armor"
    / "affinity for artifacts"
    / "bushido" SPACE Number
    / "rampage" SPACE Number
    / "soulshift" SPACE Number
    / "fading" SPACE Number
    / "devour" SPACE Number
    / "modular" SPACE Number
    / "vanishing" SPACE Number
    / "bloodthirst" SPACE Number
    / "buyback" SPACE ManaCost
    / "echo" SPACE ManaCost
    / "kicker" SPACE ManaCost
    / "multikicker" SPACE ManaCost
    / "equip" SPACE ManaCost
    / "cumulative upkeep" SPACE ManaCost
    / "replicate" SPACE ManaCost
    / "miracle" SPACE ManaCost
    / "level up" SPACE ManaCost
    ;

ColorlessCost
    = "{" Number "}"
    / "{x}"
    ;

SingleColor
    = "{b}"
    / "{u}"
    / "{g}"
    / "{r}"
    / "{w}"
    ;

HybridSingleCost
    = "{g/u}"
    / "{g/w}"
    / "{r/g}"
    / "{r/w}"
    / "{u/r}"
    / "{u/b}"
    / "{b/g}"
    / "{b/r}"
    / "{w/b}"
    ;

ManaCost
    = ColorlessCost+ SingleColor*
    / SingleColor+
    / HybridSingleCost+
    ;

BecomesTargetTrigger
    = "when @ becomes the target of a spell or ability"
    ;

BlocksTrigger
    = "whenever @ blocks"
    ;

BlockedTrigger
    = "whenever @ becomes blocked"
    ;

CastTrigger
    = "whenever " SelectPlayer " cast " SelectSpell
    / "when " SelectPlayer " cast " SelectSpell
    ;

LeavesBattlefieldTrigger
    = "when @ leaves the battlefield"
    ;

EntersGraveyardTrigger
    = "when @ is put into a graveyard from the battlefield"
    / "when @ is put into a graveyard from anywhere"
    ;

EntersBattlefieldTrigger
    = "when @ enters the battlefield"
    / "as @ enters the battlefield"
    / "whenever @ or another ally enters the battlefield under your control"
    ;

BeginningEndStepTrigger
    = "at the beginning of the end step"
    ;

BeginningUpkeepTrigger
    = "at the beginning of your upkeep"
    / "at the beginning of each player's upkeep" 
    ;

DiesTrigger
    = "when @ dies"
    ;

DealsDamageTrigger
    = "whenever @ deals" " combat"? " damage" (" to a " DamageReceiver)?
    ;

DamageReceiver
    = "itself"
    / SelectPlayer
    / SelectCreature (" or " SelectPlayer)?
    ;

AttacksTrigger
    = "whenever @ attacks"
    ;

ChangeStateAction
    = (SelectPermanent SPACE)? "can't attack or block" 
    / (SelectPermanent SPACE)? "can't attack" 
    / (SelectPermanent SPACE)? "can't block" 
    / (SelectPermanent SPACE)? "is indestructible" 
    / (SelectPermanent SPACE)? "is unblockable" 
    / (SelectPermanent SPACE)? "attacks each turn if able"
    / (SelectPermanent SPACE)? "can't be countered"
    ;

SetPTAction
    = "@'s power and toughness are each equal to the number of " SelectPermanent
    ;

SacrificeAction
    = "sacrifice" "s"? SPACE SelectPermanent (SPACE "unless you" SPACE Action)?
    ;

PoisonAction
    = "gets " Count " poison counters"
    / "gets a poison counter"
    ;

ReanimateAction
    = "put " SelectCard " from a graveyard onto the battlefield under your control"
    ;

MillAction
    = "put" "s"? " the top " Count " cards of his or her library into his or her graveyard"
    / "put" "s"? " the top card of his or her library into his or her graveyard"
    ;

DoesntUntapAction
    = SelectPermanent " doesn't untap during its controller's untap step"
    / SelectPermanent " doesn't untap during its controller's next untap step"
    / SelectPermanent " doesn't untap during your untap step"
    ;

ShuffleAction
    = "shuffle it into its owner's library"
    / "shuffle your library"
    ;

PutTokenAction
    = "put a 1/1 green saproling creature token onto the battlefield"
    / "put a 1/1 gnome artifact creature token onto the battlefield"
    / "put a token that's a copy of " SelectPermanent " onto the battlefield"
    ;

ControlAction
    = "control" "s"? SPACE SelectPermanent
    / "gain control of " SelectPermanent 
    ;

ChangeCounterAction
    = "remove a " CounterType " counter from " SelectPermanent
    / "remove " Count SPACE CounterType " counters from " SelectPermanent
    / "put a " CounterType " counter on " SelectPermanent
    / "put " Count SPACE CounterType " counters on " SelectPermanent
    ;

PayManaAction
    = "pay" "s"? SPACE ManaCost
    / ManaCost
    ;

ChangeLifeAction
    = "lose" "s"? SPACE Number " life"
    / "gain" "s"? SPACE Number " life"
    / "pay" "s"? SPACE Number " life"
    ;

SelectPlayer
    = "target player"
    / "target opponent"
    / "that player"
    / "each player"
    / "each opponent"
    / "an opponent"
    / "you"
    / "he or she"
    / "its controller"
    / "its owner"
    / "player"
    ;

ReturnAction
    = "return " SelectCard " from your graveyard to your hand" 
    / "return " SelectCard " from your graveyard to the battlefield" 
    / "return " SelectCard " to the battlefield under its owner's control" 
    ;

BounceAction
    = "return" "s"? SPACE SelectPermanent SPACE "to its owner's hand"
    / "put" "s"? SPACE SelectPermanent SPACE "on top of its owner's library"
    / "put" "s"? SPACE SelectPermanent SPACE "on the bottom of its owner's library"
    ;

PreventAction
    = "prevent the next " Number SPACE Damage " that would be dealt to " DamageReceiver 
    / "prevent all " Damage " that would be dealt to and dealt by " DamageReceiver 
    / "prevent all " Damage " that would be dealt to " DamageReceiver
    / "prevent all " Damage " that would be dealt by " DamageReceiver
    / "prevent all " Damage " that would be dealt" 
    / "if damage would be dealt to " DamageReceiver ", prevent that damage"
    ;

Damage
    = "damage"
    / "combat damage"
    ;

TapAction
    = "{t}"
    / "tap" SPACE SelectPermanent
    / "{q}"
    / "untap" SPACE SelectPermanent
    / "tap or untap" SPACE SelectPermanent
    ;

RegenerateAction
    = "regenerate" SPACE SelectPermanent
    ;

DamageAction
    = SelectPermanent " deals " Number " damage to " DamageReceiver
    / SelectPermanent " deals damage equal to its power to " DamageReceiver
    / SelectPermanent " deals damage equal to the number of " CounterType " counters on it to " DamageReceiver
    / SelectPermanent " deals damage to " DamageReceiver " equal to the number of " SelectPermanent
    / Number " damage to " DamageReceiver
    ;

EntersTapped
    = "@ enters the battlefield tapped"
    ;

EntersWithCounter
    = "@ enters the battlefield with a " CounterType " counter on it"
    / "@ enters the battlefield with " Count SPACE CounterType " counters on it"
    ;

CounterType
    = "+1/+1"
    / "-1/-1"
    / "+1/+0"
    / "charge"
    / "depletion"
    / "spore"
    / "storage"
    / "ki"
    / "verse"
    / "quest"
    / "fade"
    / "age"
    / "tide"
    / "time"
    / "fuse"
    / "page"
    ;

Count
    = "one"
    / "two"
    / "three"
    / "four"
    / "five"
    / "six"
    / "seven"
    / "eight"
    / "nine"
    / "ten"
    / "x"
    ;

Enchant
    = "enchant" SPACE RestrictedPermanent
    / "enchant player"
    ;

Duration
    = "until end of turn"
    / "for as long as " SelectPlayer " control @"
    / "as long as you control three or more artifacts"
    / "this turn"
    ;

GainAction
    = (SelectPermanent SPACE)? "has " Keyword
    / (SelectPermanent SPACE)? "have " Keyword
    / (SelectPermanent SPACE)? "gain" "s"? SPACE Keyword
    / (SelectPermanent SPACE)? "lose" "s"? SPACE Keyword
    ;

PumpAction
    = SelectCreature " get" "s"? SPACE SignedNumber "/" SignedNumber 
    ;

AddManaAction
    = "add " ManaSource " to your mana pool"
    ;

ManaSource
    = "{1}"
    / SingleColor (", " SingleColor)* ", or " SingleColor
    / SingleColor " or " SingleColor
    / SingleColor
    / "one mana of any color"
    ;

DrawAction
    = Draw " a card"
    / Draw " " Count " cards"
    ;

Draw
    = "draw" "s"?
    ;

DiscardAction
    = Discard SPACE SelectCard " at random"?
    / Discard SPACE Count SPACE RestrictedCard " at random"
    ;

Discard
    = "discard" "s"?
    ;

DestroyNoRegenAction
    = "destroy " SelectPermanent EOS SPACE NoRegen
    ;

DestroyAction 
    = "destroy " SelectPermanent
    ;

ExileAction
    = "exile " SelectPermanent
    ;

CounterAction
    = "counter " SelectSpell (" unless " SingleEffect)?
    ;

SelectOp
    = "target"
    / "all other"
    / "all"
    / "an"
    / "a"
    / "each other"
    / "each"
    / "enchanted"
    / "equipped"
    / "the"
    / "that"
    / "other"
    ;

SelectCard
    = "@"
    / "it"
    / SelectOp SPACE RestrictedCard
    ;

SelectCreature
    = "@"
    / "it"
    / (SelectOp SPACE)? RestrictedCreature
    ;

SelectPermanent
    = "@"
    / "it"
    / (SelectOp SPACE)? RestrictedPermanentCombination
    ;

SelectSpell
    = "@"
    / "it"
    / SelectOp SPACE RestrictedSpell
    ;

RestrictedPermanentCombination
    = RestrictedPermanent ", " RestrictedPermanent ", or " RestrictedPermanent
    / RestrictedPermanent ", " RestrictedPermanent ", and " RestrictedPermanent
    / RestrictedPermanent " or " RestrictedPermanent
    / RestrictedPermanent " and " RestrictedPermanent
    / RestrictedPermanent 
    ;

RestrictedPermanent
    = (PermanentRestriction SPACE)? Permanent (SPACE PermanentRestriction)?
    / RestrictedCreature
    / RestrictedLand
    / RestrictedArtifact
    / RestrictedEnchantment
    / RestrictedTribal
    ;

RestrictedArtifact
    = Artifact (SPACE PermanentRestriction)?
    / (PermanentRestriction SPACE)? Artifact (SPACE PermanentRestriction)?
    ;

RestrictedEnchantment
    = (PermanentRestriction SPACE)? Enchantment (SPACE PermanentRestriction)?
    ;

RestrictedLand
    = (LandRestriction SPACE)? Land (SPACE LandRestriction)?
    ;

RestrictedTribal
    = Tribal
    ;

RestrictedSpell
    = (SpellRestriction SPACE)? Spell (SPACE SpellRestriction)?
    ;

SpellRestriction
    = "creature"
    / "white, blue, black, or red"
    / "red or green"
    / "red"
    / "white"
    / "black"
    / "blue"
    / "green"
    / "multicolored"
    / "noncreature"
    / "enchantment"
    / "spirit or arcane"
    / "treefolk"
    / "vampire creature"
    / "wizard"
    / "artifact"
    / "during an opponent's turn"
    / "instant or sorcery"
    / "instant"
    / "sorcery"
    / "with infect"
    / "with converted mana cost " Number " or less"
    / "with converted mana cost " Number " or greater"
    / "with converted mana cost " Number
    ;

RestrictedCard
    = (CardRestriction SPACE)? Card
    ;

CardRestriction
    = "exiled"
    / SpellRestriction
    ;

RestrictedCreature
    = (CreatureRestriction SPACE)? Creature (SPACE CreatureRestriction)?
    ;

PermanentRestriction
    = "black or red"
    / "green or white"
    / "black or red"
    / "blue or black"
    / "red or green"
    / "blue"
    / "white"
    / "green"
    / "red"
    / "black"
    / "tapped"
    / "untapped"
    / "noncreature"
    / SelectPlayer " control" "s"?
    / SelectPlayer " don't control"
    / "nonblack"
    / "nonartifact, nonblack"
    / "nonartifact"
    / "artifact"
    / "other than @"
    / "with converted mana cost " Number " or less"
    / "with converted mana cost " Number " or greater"
    / "with converted mana cost " Number
    ;

CreatureRestriction
    = "with power " Number " or less"
    / "with power " Number " or greater"
    / "with " Keyword
    / "without " Keyword
    / "tapped or blocking"
    / "blocked"
    / "attacking or blocking"
    / "attacking"
    / Tribal
    / PermanentRestriction
    ;

LandRestriction
    = "nonbasic"
    / "basic"
    / PermanentRestriction
    ;

Tribal
    = "spirit" "s"?
    / "wall" "s"?
    / "human"
    / "elf"
    / "golem"
    / "bird"
    / "goblin"
    / "saproling"
    / "ally"
    / "scarecrow"
    / "pentavite"
    / "wizard" "s"?
    / "beast" "s"?
    / "cleric" "s"?
    / "zombie" "s"?
    / "sliver" "s"?
    / "non-vampire, non-werewolf, non-zombie"
    ;

Creature
    = "creature" "s"?
    ;

Artifact
    = "artifact" "s"?
    / "equipment" "s"?
    ;

Land
    = "land" "s"?
    / "swamp" "s"?
    / "island" "s"?
    / "forest" "s"?
    / "mountain" "s"?
    / "plains"
    ;

Enchantment
    = "enchantment" "s"?
    ;

Permanent
    = "permanent" "s"?
    ;

Card
    = "card" "s"?
    ;

Spell
    = "spell" "s"?
    ;

NoRegen 
    = "it can't be regenerated"
    / "they can't be regenerated"
    ;

SignedNumber
    = Sign Number
    ;

Number 
    = [0-9]+
    / "that much"
    / "x"
    ;

Sign
    = "+"
    / "-"
    ;

SPACE 
    = " "
    ;
    
EOS 
    = "."
    ;

EOR
    = !_
    ;
